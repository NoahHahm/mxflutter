group 'com.mojitox.mxflutter'
version '1.0'

buildscript {
    apply from: file("config.gradle")
    ext.kotlin_version = '1.3.50'
    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.5.0'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

rootProject.allprojects {
    repositories {
        google()
        jcenter()
    }
}

apply plugin: 'com.android.library'
apply plugin: "kotlin-android"

android {
    compileSdkVersion 28

    defaultConfig {
        minSdkVersion 17
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        buildConfigField "boolean", "USE_V8", "$USE_V8"
        externalNativeBuild {
            cmake {
                cppFlags ""
            }
        }
    }

    /** 编译mxflutter so时反注释掉此处,编译完恢复 **/
//    externalNativeBuild {
//        cmake {
//            path "CMakeLists.txt"
//        }
//
//    }

    lintOptions {
        disable 'InvalidPackage'
    }

    //java版本8
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    //如果so动态下发/使用x5，这里就不要打进安装包
    if (!USE_V8 || USE_V8_SO_DOWNLOAD) {
        packagingOptions {
            exclude 'lib/armeabi-v7a/libj2v8.so'
            exclude 'lib/arm64-v8a/libj2v8.so'
            exclude 'lib/x86/libj2v8.so'
            exclude 'lib/x86_64/libj2v8.so'
        }
    }

    buildTypes {
        release {
            consumerProguardFiles 'proguard-rules.pro'
            externalNativeBuild {
                cmake {
                    cppFlags "-DTENCENT_SHELL=1", "-DNOLOG=1"
                }
            }
        }
        debug{
            externalNativeBuild {
                cmake {
                    cppFlags "-DTENCENT_SHELL=0"
                }
            }
        }
    }

    sourceSets {
        main {
            jniLibs.srcDirs += 'libs'
            java {
                if (USE_V8) {
                    srcDirs = [
                            'src/main/java',
                            'src/main/v8',
                    ]
                } else {
                    srcDirs = [
                            'src/main/java',
                            'src/main/x5'
                    ]
                }
            }
        }
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    if (!USE_V8) {
        api 'com.tencent.tbs.tbssdk:sdk:43939'
    }
    implementation "androidx.annotation:annotation:1.1.0"
}

